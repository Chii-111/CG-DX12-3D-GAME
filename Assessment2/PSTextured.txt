Texture2D tex : register(t0);
SamplerState samplerLinear : register(s0);

cbuffer LightBuffer : register(b0)
{
    float3 cameraPos; 
    float padding1;
    float3 lightDir;  
    float padding2;
    float3 lightColor; 
    float padding3;
    float ambientStrength; 
};

struct PS_INPUT
{
    float4 Pos : SV_POSITION;
    float3 Normal : NORMAL;
    float2 TexCoords : TEXCOORD;
    float3 WorldPos : POSITION; 
};

float4 PS(PS_INPUT input) : SV_Target0
{
    float4 texColor = tex.Sample(samplerLinear, input.TexCoords);
    if(texColor.a < 0.1) discard; 

    float3 N = normalize(input.Normal);
    float3 L = normalize(-lightDir); 
    float3 V = normalize(cameraPos - input.WorldPos); 
    
    float3 ambient = ambientStrength * lightColor;

    float diff = max(dot(N, L), 0.0);
    float3 diffuse = diff * lightColor;

    float3 halfwayDir = normalize(L + V);
    float spec = pow(max(dot(N, halfwayDir), 0.0), 32.0); 
    float3 specular = float3(0.5, 0.5, 0.5) * spec; 

    float3 finalColor = (ambient + diffuse) * texColor.rgb + specular;

    return float4(finalColor, texColor.a);
}